apply plugin: 'java'
sourceCompatibility = 1.6

apply plugin: 'maven'
group="org.asoem"
version="0.2.0"


apply plugin: 'application'
mainClassName = "org.asoem.greyfish.cli.ScenarioRunner"

repositories {
    flatDir { dirs 'lib' }
    mavenLocal()
    mavenCentral()
    maven { url "http://scala-tools.org/repo-releases" } // for org.scala-lang:scala-library
    maven { url "http://mirrors.ibiblio.org/pub/mirrors/maven2/" }
}

dependencies {
    compile 'org.scala-lang:scala-library:2.9.1-1',
            'org.codehaus.jsr166-mirror:jsr166y:1.7.0',
            'org.apache.commons:commons-math3:3.0',
            'commons-pool:commons-pool:1.6',
            'com.google.inject.extensions:guice-assistedinject:3.0',
            'org.jdesktop.bsaf:bsaf:1.9.2',
            'jfree:jfreechart:1.0.13',
            'org.slf4j:slf4j-api:1.6.4',
            'org.simpleframework:simple-xml:2.6.2',
            'com.h2database:h2:1.3.166',
            'javolution:javolution:5.5.1',
            'org.asoem:kdtree:1.1.0',
            'com.explodingpixels:macwidgets:0.9.5-SNAPSHOT',
            jgoodies_dependency("binding", "2.6.1"),
            jgoodies_dependency("forms", "1.5.1"),
            jgoodies_dependency("validation", "2.4.1"),
            jgoodies_dependency("common", "1.3.1"),
            'org.codehaus.jackson:jackson-mapper-asl:1.9.6',
            'commons-cli:commons-cli:1.2'


    compile (
            [group: 'com.google.guava', name: 'guava', version: '11.0.2', transitive: true],
            [group: 'com.google.inject', name: 'guice', version: '3.0', transitive: true],
            [group: 'org.apache.commons', name: 'commons-jexl', version: '2.1.1', transitive: true],
            [group: 'jfree', name: 'jfreechart', version: '1.0.13', transitive: true],
            [group: 'org.mongodb', name: 'mongo-java-driver', version: '2.7.3', transitive: true],
            [group: 'org.jongo', name: 'jongo', version: '0.1', transitive: true]
    )

    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.0', transitive: true

    testCompile "org.easytesting:fest-assert:1.4", "junit:junit:4.10", "org.mockito:mockito-all:1.9.0"
}

String jgoodies_dependency(String name, String version) {

    String artifactId = "com.jgoodies:jgoodies-${name}:${version}"
    String libFileName = "jgoodies-${name}-${version}.jar"

    File libraryFile = new File('lib/' + libFileName.toString());

    if (! libraryFile.exists()) {
        String remoteZipUrl = "http://www.jgoodies.com/download/libraries/${name}/jgoodies-${name}-${version.replaceAll(/\./, '_')}.zip"
        //noinspection GroovyAssignabilityCheck
        File localTempZipFile = new File(rootDir, "jgoodies-${name}-${version}.zip".toString())

        if (! localTempZipFile.exists() ) {
            logger.debug "Downloading ${remoteZipUrl} to ${localTempZipFile.getAbsolutePath()}"
            ant.get(src: remoteZipUrl, dest: localTempZipFile.getPath())
        }

        assert localTempZipFile.exists() : "Could not download ${remoteZipUrl} to ${localTempZipFile.getAbsolutePath()}"

        File dir = new File("lib")
        if (!dir.isDirectory()) {
            dir.mkdirs()
        }

        logger.debug "Copying all jars in ${localTempZipFile} to './lib'"
        copy {
            from zipTree(localTempZipFile).matching{ include '**/*.jar' }.getFiles()
            into 'lib'
        }

        logger.debug "Removing ${localTempZipFile}"
        localTempZipFile.delete()
    }

    return artifactId
}
