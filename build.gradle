subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.6

    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    test {
        include '**/*Test.class'
        exclude '**/Abstract*.class'
    }

    task integrationTest(type: Test, dependsOn: test) {
        include '**/*IT.class'
        exclude '**/Abstract*.class'
    }

    task acceptanceTest(type: Test, dependsOn: integrationTest) {
        include '**/*AT.class'
        exclude '**/Abstract*.class'
    }
}

